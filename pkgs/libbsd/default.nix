{
  lib,
  stdenv,
  fetchurl,
  autoreconfHook,
  libmd,
  gitUpdater,
  runUnitTests,
}:

stdenv.mkDerivation (finalAttrs: {
  pname = "libbsd";
  version = "0.12.2";

  src = fetchurl {
    url = "https://libbsd.freedesktop.org/releases/${finalAttrs.pname}-${finalAttrs.version}.tar.xz";
    hash = "sha256-uIzJFj0MZSqvOamZkdl03bocOpcR248bWDivKhRzEBQ=";
  };

  outputs = [
    "out"
    "dev"
    "man"
  ];

  nativeBuildInputs = [ autoreconfHook ];
  propagatedBuildInputs = [ libmd ];

  patches = [
    # `strtonum(3)` is not available on our default SDK version.
    # https://gitlab.freedesktop.org/libbsd/libbsd/-/issues/30
    ./darwin-enable-strtonum.patch
  ];

  passthru = {
    updateScript = gitUpdater {
      # No nicer place to find latest release.
      url = "https://gitlab.freedesktop.org/libbsd/libbsd.git";
    };

    tests = {
      unittests = runUnitTests finalAttrs.finalPackage;
    };
  };

  # Fix undefined reference errors with version script under LLVM.
  configureFlags = lib.optionals (
    stdenv.cc.bintools.isLLVM && lib.versionAtLeast stdenv.cc.bintools.version "17"
  ) [ "LDFLAGS=-Wl,--undefined-version" ];

  meta = {
    description = "Common functions found on BSD systems";
    homepage = "https://libbsd.freedesktop.org/";
    license = with lib.licenses; [
      beerware
      bsd2
      bsd3
      bsdOriginal
      isc
      mit
    ];
    platforms = lib.platforms.unix;
    # See architectures defined in src/local-elf.h.
    badPlatforms = lib.platforms.microblaze;
    identifiers.cpeParts = lib.meta.cpeFullVersionWithVendor "freedesktop" finalAttrs.version;
  };
})
