{
  lib,
  stdenv,
  fetchFromGitLab,
  pkg-config,
  meson,
  ninja,
  zlib,
  hwdata,
  libdrm,
}:

stdenv.mkDerivation (finalAttrs: {
  pname = "libpciaccess";
  version = "0.18.1";

  outputs = [
    "out"
    "include"
  ];
  outputInclude = "include";

  src = fetchFromGitLab {
    domain = "gitlab.freedesktop.org";
    group = "xorg";
    owner = "lib";
    repo = "libpciaccess";
    tag = "libpciaccess-${finalAttrs.version}";
    hash = "sha256-gyzRsF0SDb89aF0vmCY2OAg15i1BtwjR2MTJy4kbsbo=";
  };

  nativeBuildInputs = [
    pkg-config
    meson
    meson.configurePhaseHook
    ninja
  ];

  buildInputs = [
    zlib
  ];

  mesonFlags = [
    (lib.mesonOption "pci-ids" "${hwdata}/share/hwdata")
    (lib.mesonEnable "zlib" true)
  ];

  passthru.tests = {
    inherit libdrm;
  };

  meta = {
    description = "Generic PCI access library";
    homepage = "https://gitlab.freedesktop.org/xorg/lib/libpciaccess";
    license = with lib.licenses; [
      mit
      isc
      x11
    ];
    platforms = lib.platforms.linux ++ lib.platforms.freebsd ++ lib.platforms.openbsd;
  };
})
