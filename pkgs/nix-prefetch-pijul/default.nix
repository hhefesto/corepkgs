{
  lib,
  stdenv,
  makeWrapper,
  bash,
  coreutils,
  sed,
  gawk,
  pijul,
  cacert,
  jq,
}:
stdenv.mkDerivation (finalAttrs: {
  pname = "nix-prefetch-pijul";
  version = "1.0.0";

  nativeBuildInputs = [ makeWrapper ];
  buildInputs = [ bash ];

  dontUnpack = true;

  installPhase = ''
    install -vD ${./nix-prefetch-pijul.sh} $out/bin/$pname;
    wrapProgram $out/bin/$pname --prefix PATH : ${
      lib.makeBinPath [
        gawk
        pijul
        cacert
        jq
        coreutils
        sed
      ]
    } --set HOME /homeless-shelter
  '';

  preferLocalBuild = true;

  meta = {
    description = "Script used to obtain source hashes for fetchpijul";
    license = lib.licenses.lgpl21Plus;
    platforms = lib.platforms.unix;
    mainProgram = "nix-prefetch-pijul";
  };
})
