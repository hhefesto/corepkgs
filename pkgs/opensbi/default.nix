{
  lib,
  stdenv,
  fetchFromGitHub,
  python3,
  withPlatform ? "generic",
  withPayload ? null,
  withFDT ? null,
}:

stdenv.mkDerivation rec {
  pname = "opensbi";
  version = "1.8.1";

  src = fetchFromGitHub {
    owner = "riscv-software-src";
    repo = "opensbi";
    tag = "v${version}";
    hash = "sha256-nD22UZfH0rJECHMDwd9ATyLz44cFHqcFH7N6piK8hog=";
  };

  postPatch = ''
    patchShebangs ./scripts
  '';

  nativeBuildInputs = [ python3 ];

  installFlags = [ "I=$(out)" ];

  makeFlags = [
    "PLATFORM=${withPlatform}"
  ]
  ++ lib.optionals (withPayload != null) [ "FW_PAYLOAD_PATH=${withPayload}" ]
  ++ lib.optionals (withFDT != null) [ "FW_FDT_PATH=${withFDT}" ];

  dontStrip = true;
  dontPatchELF = true;

  meta = {
    description = "RISC-V Open Source Supervisor Binary Interface";
    homepage = "https://github.com/riscv-software-src/opensbi";
    license = lib.licenses.bsd2;
    platforms = [
      "riscv64-linux"
      "riscv32-linux"
    ];
    identifiers.cpeParts = lib.meta.cpeFullVersionWithVendor "opensbi_project" version;
  };
}
